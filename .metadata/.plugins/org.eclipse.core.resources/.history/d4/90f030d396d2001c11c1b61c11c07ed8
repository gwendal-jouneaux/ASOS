grammar fr.irisa.diverse.adaptivesemantics.AdaptSem with org.eclipse.xtext.common.Terminals

import "http://www.diverse-team.fr/adaptivesemantics" 
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

AdaptiveSemantics returns AdaptiveSemantics:
	model=Model
	imports+=Import*
	rules+=Rule*;





Model returns Model:
	'model' importURI=EString;

Import returns Import:
	'import' importURI=EString;

Rule returns Rule:
	'rule' name=ID ','
		conclusion=Conclusion
	('where'
		(premises+=Premise | conditions+=Condition)
		("," (premises+=Premise | conditions+=Condition))* )?
	;





Conclusion returns Conclusion:
	from=DefConfiguration '->' to=ConclusionOutput
;



ConclusionOutput returns ConclusionOutput:
	 TermRef | SemanticDomainUpdate | SemanticDomainAccess
;





Premise returns Premise:
	from=TermRef '->' to=TermDef
;





Condition returns Condition:
	{Condition}
	oclPredicate=EString;
	
	
	


DefConfiguration returns DefConfiguration:
	concept=[ecore::EClassifier|FQN]
	"("
		(childs+=TermDef ("," childs+=TermDef)*)?
	")"
;
	
RefConfiguration returns RefConfiguration:
	concept=[ecore::EClassifier|FQN] "("
		(childs+=TermRef ("," childs+=TermRef)*)?
	")"
;

SymbolDef returns SymbolDef:
	{SymbolDef}
	name=ID
;

SymbolRef returns SymbolRef:
	{SymbolRef}
	def=[SymbolDef]
;

TermDef returns TermDef:
	SymbolDef | DefConfiguration | ListDef
;

TermRef returns TermRef:
	SymbolRef | RefConfiguration | ListRef | ListAppend
;

ListDef returns ListDef:
	"[" head=SymbolDef "|" tail=SymbolDef "]"
;

ListRef returns ListRef:
	"[" head=SymbolRef "|" tail=SymbolRef "]"
;

ListAppend returns ListAppend:
	"[" head=TermRef "+" tail=TermRef "]"
;


SemanticDomainAccess returns SemanticDomainAccess:
	path=[ecore::EStructuralFeature|AttributeFQN]
;

SemanticDomainUpdate returns SemanticDomainUpdate:
	path=[ecore::EStructuralFeature|AttributeFQN] "=" value=TermRef
;





EString returns ecore::EString:
	STRING | ID;
	
FQN: ID ("." ID)*;

AttributeFQN: ID ("." ID)+;


	
	
	
	
	
	
	