grammar fr.irisa.diverse.adaptivesemantics.AdaptSem with org.eclipse.xtext.common.Terminals

import "http://www.diverse-team.fr/adaptivesemantics" 
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

AdaptiveSemantics returns AdaptiveSemantics:
	model=Model
	imports+=Import*
	rules+=Rule*;

Model returns Model:
	'model' importURI=EString;

Import returns Import:
	'import' importURI=EString;

Rule returns Rule:
	'rule' name=EString ','
		conclusion=Reduction
	('where'
		(premises+=Reduction | conditions+=Condition)+)?
	;

EString returns ecore::EString:
	STRING | ID;
	
FQN: ID ("." ID)*;

Reduction returns Reduction:
	from=Configuration '->' to=Configuration;

Condition returns Condition:
	{Condition}
	oclPredicate=EString;

Configuration returns Configuration:
	concept=[ecore::EClassifier|FQN]
	"("
		(childs+=EString ("," childs+=EString)*)?
	")"
	;