grammar fr.irisa.diverse.adaptivesemantics.AdaptSem with org.eclipse.xtext.common.Terminals

import "http://www.diverse-team.fr/adaptivesemantics" 
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

AdaptiveSemantics returns AdaptiveSemantics:
	model=Model
	imports+=Import*
	rules+=Rule*;





Model returns Model:
	'model' importURI=EString;

Import returns Import:
	'import' importURI=EString;

Rule returns Rule:
	'rule' name=ID ','
		conclusion=Conclusion
	('where'
		(premises+=Premise | conditions+=Condition)
		("," (premises+=Premise | conditions+=Condition))* )?
	;





Conclusion returns Conclusion:
	from=DefConfiguration '->' to=ConclusionOutput
;



ConclusionOutput returns ConclusionOutput:
	 SemanticDomainUpdate | SemanticDomainAccess  | TermRef
;





Premise returns Premise:
	from=TermRef '->' to=TermDef
;





Condition returns Condition:
	{Condition}
	oclPredicate=EString;
	
	
	


DefConfiguration returns DefConfiguration:
	concept=[ecore::EClassifier|FQN]
	"("
		(childs+=SymbolDef ("," childs+=SymbolDef)*)?
	")"
;
	
RefConfiguration returns RefConfiguration:
	concept=[ecore::EClassifier|FQN]
	"("
		(childs+=SymbolRef ("," childs+=SymbolRef)*)?
	")"
;

SymbolDef returns SymbolDef:
	{SymbolDef}
	name=ID
;

SymbolRef returns SymbolRef:
	{SymbolRef}
	def=[SymbolDef]
;

TermDef returns TermDef:
	SymbolDef | DefConfiguration
;

TermRef returns TermRef:
	SymbolRef | RefConfiguration
;





SemanticDomainAccess returns SemanticDomainAccess:
	path=[ecore::EStructuralFeature|FQN]
;

SemanticDomainUpdate returns SemanticDomainUpdate:
	path=[ecore::EStructuralFeature|FQN] "=" value=TermRef
;





EString returns ecore::EString:
	STRING | ID;
	
FQN: ID ("." ID)*;


	
	
	
	
	
	
	